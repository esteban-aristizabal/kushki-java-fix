apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'
apply plugin: 'pmd'
apply plugin: 'findbugs'

group 'com.kushkipagos'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/public"
    }
}


configurations {
    unitTestCompile.extendsFrom testCompile
    unitTestRumtime.extendsFrom testRuntime

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}


dependencies {
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.1',
            'org.glassfish.jersey.core:jersey-client:2.22.2'

    testCompile 'junit:junit:4.11',
            'org.apache.commons:commons-lang3:3.4',
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-all:2.0.2-beta',
            'pl.pragmatists:JUnitParams:1.0.4'
}

task unitTest(type: Test) {
    include 'com/kushkipagos/unit/**'
}

task integrationTest(type: Test) {
    include 'com/kushkipagos/integration/**'
}

jar {
    archiveName 'Kushki.jar'
}

pmd {
    targetJdk = '1.7'
    toolVersion = '4.3'
}

project.ext.pmdRuleSets = [
        'basic',
        'braces',
        'clone',
        'codesize',
        'logging-java',
        'migrating',
        'strictexception',
        'typeresolution',
        'unusedcode',
]

pmdMain {
    ruleSets = pmdRuleSets + ['coupling', 'imports', 'strings']
    ruleSetFiles = files('config/pmd/pmd.xml')
}

pmdTest {
    ruleSets = pmdRuleSets
    ruleSetFiles = files('config/pmd/pmd.xml')
}

findbugs {
    toolVersion = '3.0.0'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    testLogging {
        showStandardStreams = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.3'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'API de Java para realizar transacciones con el backend de Kushki',
                'Implementation-Version': version,
                'Main-Class': 'com.kushkipagos.Kushki'
    }
    archiveName 'Kushki.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
