import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath files('gradle/gradle-trust-all.jar')
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

plugins {
    id "org.sonarqube" version "2.5"
}


group 'com.kushkipagos'
version '1.3.1'

apply from: 'deploy.gradle'
apply plugin: "com.jfrog.bintray"
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'maven-publish'
apply plugin: 'application'
apply plugin: 'jacoco'

sonarqube {
    properties {
        property "sonar.projectName", "Java :: Simple Project :: SonarQube Scanner for Gradle"
        property "sonar.projectKey", "org.sonarqube:java-gradle-simple"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    unitTestCompile.extendsFrom testCompile
    unitTestRumtime.extendsFrom testRuntime

    integrationTestCompile.extendsFrom testCompile
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.1',
            'com.googlecode.json-simple:json-simple:1.1.1',
            'org.glassfish.jersey.core:jersey-client:2.22.2',
            'com.mashape.unirest:unirest-java:1.4.9'


    testCompile 'junit:junit:4.11',
            'org.apache.commons:commons-lang3:3.4',
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-all:2.0.2-beta',
            'com.googlecode.json-simple:json-simple:1.1.1',
            'org.skyscreamer:jsonassert:1.5.0'
    'pl.pragmatists:JUnitParams:1.0.4'
}

task printVersion {
    println project.version
}

task unitTest(type: Test) {
    include 'com/kushkipagos/unit/**'
}

task integrationTest(type: Test) {
    include 'com/kushkipagos/integration/**'
}

pmd {
    targetJdk = '1.7'
    toolVersion = '4.3'
}

project.ext.pmdRuleSets = [
        'basic',
        'braces',
        'clone',
        'codesize',
        'logging-java',
        'migrating',
        'strictexception',
        'typeresolution',
        'unusedcode',
]

pmdMain {
    ruleSets = pmdRuleSets + ['coupling', 'imports', 'strings']
    ruleSetFiles = files('config/pmd/pmd.xml')
}

pmdTest {
    ruleSets = pmdRuleSets
    ruleSetFiles = files('config/pmd/pmd.xml')
}

test {
    ignoreFailures = true
}

findbugs {
    toolVersion = '3.0.0'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    outputs.upToDateWhen { false }
    testLogging {
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR, TestLogEvent.STANDARD_OUT
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "https://opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Andres Martinez"
            email "andresm@kushkipagos.com"
        }
    }

    scm {
        url "https://github.com/Kushki/kushki-java"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.kushkipagos'
            artifactId 'kushkipagos'
            version '1.3.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java backend library')
                root.appendNode('name', 'kushkipagos')
                root.appendNode('url', 'https://github.com/Kushki/kushki-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_API_KEY"
    publications = ['MyPublication']
    pkg {
        userOrg = user
        repo = 'maven'
        name = 'kushkipagos'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Kushki/kushki-java.git'
        publicDownloadNumbers = false
    }
}