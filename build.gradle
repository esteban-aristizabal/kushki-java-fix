apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'
apply plugin: 'pmd'
apply plugin: 'findbugs'

group 'com.kushkipagos'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/public"
    }
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java/com/kushkipagos/unit', 'src/test/java/com/kushkipagos/commons']
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = ['src/test/java/com/kushkipagos/integration', 'src/test/java/com/kushkipagos/commons']
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}


dependencies {
    compile(
        'com.sun.jersey:jersey-client:1.19',
        'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.1'
    )
    testCompile(
        'junit:junit:4.11',
        'org.apache.commons:commons-lang3:3.4',
        'org.hamcrest:hamcrest-all:1.3',
        'org.mockito:mockito-all:2.0.2-beta',
        'pl.pragmatists:JUnitParams:1.0.4'
    )
}

test {
    dependsOn pmdMain, pmdTest, findbugsMain, findbugsTest
    testClassesDir = sourceSets.test.output.classesDir
    classpath = sourceSets.test.runtimeClasspath
    outputs.upToDateWhen { false }
}

pmd {
    targetJdk = '1.7'
    toolVersion = '4.3'
}

project.ext.pmdRuleSets = [
    'basic',
    'braces',
    'clone',
    'codesize',
    'logging-java',
    'migrating',
    'strictexception',
    'typeresolution',
    'unusedcode',
]

pmdMain {
    ruleSets = pmdRuleSets + ['coupling', 'imports', 'strings']
    ruleSetFiles = files('config/pmd/pmd.xml')
}

pmdTest {
    ruleSets = pmdRuleSets
    ruleSetFiles = files('config/pmd/pmd.xml')
}

findbugs {
    toolVersion = '3.0.0'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.3'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'kushki-java'
                packaging 'jar'
                description 'Kushki Java bindings'
                url 'https://github.com/Senescyt/sniese-commons.git'
                scm {
                    url 'scm:https://github.com/Kushki/kushki-java.git'
                    connection 'scm:https://github.com/Kushki/kushki-java.git'
                    developerConnection 'scm:https://github.com/Kushki/kushki-java.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://choosealicense.com/licenses/mit'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'kushki'
                        name 'Kushki Pagos'
                    }
                }
            }
        }
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test