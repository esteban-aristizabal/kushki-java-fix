repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'

ext {
    pkgName = 'kushkipagos'
    groupName = 'com.kushkipagos'
    pkgDesc = 'Librer√≠a Java para integrarse con Kushki'
    githubRepo = 'github.com/Kushki/kushki-java'
    pkgLicense = 'MIT'
    pkgLicenseUrl = 'https://github.com/Kushki/kushki-java/blob/master/LICENSE.txt'
    bintrayUser = System.getenv('BINTRAY_USER')
    bintrayKey = System.getenv('BINTRAY_API_KEY')
    mavenCentralUser = System.getenv('MAVEN_CENTRAL_USER')
    mavenCentralToken = System.getenv('MAVEN_CENTRAL_TOKEN')
    versionTag = version.toString()
    githubUrl = 'https://' + githubRepo
    scmUrl = 'scm:git:git://' + githubRepo + '.git'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.version = versionTag
        pom.groupId = groupName
        pom.artifactId = pkgName
        pom.project {
            name pkgName
            description pkgDesc
            url githubUrl
            organization {
                name 'Kushki S.A.'
                url 'https://www.kushkipagos.com'
            }
            developers {
                developer {
                    id 'thoughtworks'
                    name 'ThoughtWorks'
                    email 'kushki@thoughtworks.com'
                }
            }
            scm {
                url githubUrl
                connection scmUrl
                developerConnection scmUrl
            }
            licenses {
                license {
                    name pkgLicense
                    url pkgLicenseUrl
                    distribution 'repo'
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = pkgName
        userOrg = user
        licenses = [pkgLicense]
        publicDownloadNumbers = false
        vcsUrl = githubUrl + '.git'
        desc = pkgDesc
        websiteUrl = githubUrl
        issueTrackerUrl = githubUrl + '/issues'
        version {
            name = versionTag
            desc = pkgDesc
            vcsTag = 'v' + versionTag
            released = new Date()
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = mavenCentralUser
                password = mavenCentralToken
                close = '1'
            }
        }
    }
}
